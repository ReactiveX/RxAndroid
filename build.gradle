buildscript {
  repositories { jcenter() }
  dependencies {
    classpath 'com.netflix.nebula:gradle-rxjava-project-plugin:1.12.+'
    classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:1.12.+'
  }
  configurations.classpath.resolutionStrategy {
    // The above dependency will bring in guava, which then pollutes child projects
    force 'com.google.guava:guava:15.0'
  }

}

apply plugin: 'rxjava-project'
bintrayUpload.enabled = false

def libraryModules = subprojects.findAll { it.name != 'sample-app' }
configure(libraryModules) {
  apply plugin: 'rxjava-project'
  apply plugin: 'provided-base'

  version = rootProject.version

  dependencies {
    compile "io.reactivex:rxjava:$rxJavaVersion"
    provided "com.google.android:android:4.0.1.2"
    provided "com.google.android:support-v4:r7"

    // testing
    testCompile "junit:junit-dep:4.10"
    testCompile "org.mockito:mockito-core:1.8.5"
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "com.google.guava:guava:18.0"
    testCompile "nl.jqno.equalsverifier:equalsverifier:1.5"
    testCompile("org.robolectric:robolectric:2.3") {
      exclude group: "com.android.support"
    }
  }

  test {
    testLogging {
      exceptionFormat "full"
      events "started"
      displayGranularity 2
    }
  }
}

